

phtownall,56,342,5	Script	Moeda Poring	501,{

	.@n$ = "[^DAA520Coordenadora do Evento^000000]";

	mes .@n$;

	if( !$EVENT_STATE[EVENTID_PORINGPOINT] )
	{
		mes "Olá, o evento não está aberto, mas posso lhe passar informações sobre o mesmo.";
		for(.@i = 0; .@i < getarraysize(.idItens); .@i++)
					delitem .idItens[.@i],countitem(.idItens[.@i]);
	}
	else
		mes "Olá, veio participar do meu evento?";

	.@menu$ = ( $EVENT_STATE[EVENTID_PORINGPOINT] ? "^FF8C00Participar^000000" : "^FF0000Participar^000000" ) + ":Informações:Último Vencedor";

	switch( select( .@menu$ ) )
	{
		case 1:
			next;
			if( !$EVENT_STATE[EVENTID_PORINGPOINT] )
			{
				mes .@n$;
				mes "Como eu disse, o evento está fechado.";
				close;
			}

			mes .@n$;
			mes "Muito bem, boa sorte lá dentro!";
			close2;

			if( !@MP_TELEPORTE )
			{
				for(.@i = 0; .@i < getarraysize(.idItens); .@i++)
					delitem .idItens[.@i],countitem(.idItens[.@i]);

				PONTOS_PORING = 0;
			}

			@MP_TELEPORTE = 0;

			warp .map$,159,137;
			end;

		case 2:
			break;

		case 3:
			next;
			mes .@n$;
			mes "O último vencedor do Moeda Poring foi " + $MP_LASTOWNER$ + ".";
			close;
	}

	next;
	mes .@n$;
	mes "O que deseja saber sobre o evento?";
	switch(select("Regras Gerais:Itens do Evento:Recompensas"))
	{
		case 1:
			next;
			mes .@n$;
			mes "O objetivo do jogo é acumular ^FF8C0010 pontos de Moeda Poring^000000.";
			mes "No mapa do evento aleatoriamente irá surgir vários Porings de tempo em tempos.";
			mes "Cada Poring irá ter uma chance de dropar um item do evento.";
			next;
			mes .@n$;
			mes "Os itens que podem ser obtidos são:";
			mes " ";

			for( .@i = 0; .@i < getarraysize(.idItens); .@i++ )
				mes sprintf( "^696969[%d]:^000000 %s", ( .@i + 1 ), getitemname( .idItens[.@i] ) );

			next;
			mes .@n$;
			mes "Aquele que conseguir 10 pontos com a [Moeda Poring] consumindo o item irá ganhar o evento e a recompensa!";
			next;
			mes .@n$;
			mes "É preciso ter estratégia de quando utilizar os itens na hora certa, então estude os itens antes de jogar!";
			next;
			mes .@n$;
			mes "Ah, não é possível utilizar habilidades lá dentro, então cuidado com o efeito de certos itens para não ficar tiltado!";
			close;

		case 2:
			next;
			mes .@n$;
			mes "Ah sim, clique no item que deseja obter informação!";

			for( .@i = 0; .@i < getarraysize(.idItens); .@i++ )
				mes sprintf("^696969[%d]:^000000 <ITEM>%s<INFO>%d</INFO></ITEM>", ( .@i + 1 ), getitemname( .idItens[.@i] ), .idItens[.@i] );
			close;

		case 3:
			next;
			mes .@n$;
			mes "A recompensa para quem conseguir os 10 pontos primeiro são:";
			mes "^696969[*]:^000000 75 a 120 Pontos de Evento";
			mes "^696969[*]:^000000 100 Pontos de Cash";
			close;
	}
	end;


	OnInit:
		setarray .idItens[0],29509,29510,29511,29512,29513,29514,29515,29516,29517,29518;

		.map$ = "morocc";
		end;
}

morocc,160,138,5	script	Saída do Evento#zp	501,{

	.@n$ = "[^DAA520Coordenadora do Evento^000000]";

	mes .@n$;
	mes "Deseja saír do evento? Lembre-se de que seus pontos e itens são removidos ao saír da arena.";
	if(select("Sim:Não")==2) close;
	warp "Save",0,0;
	end;

	OnStart:
		killmonster "morocc","Saída do Evento#zp::OnPoring";
		killmonster "morocc","Saída do Evento#zp::OnPoringTrap";
		
		removemapflag "morocc",mf_pvp;
		setmapflag "morocc",mf_noskill;
		
		while( $EVENT_STATE[EVENTID_PORINGPOINT] && .@none < 5 )
		{
			if( mobcount( "morocc", "" ) < 30 )
				monster "morocc",0,0,"Poring :3",1002,rand(1,7),"Saída do Evento#zp::OnPoring";

			sleep 15000;
			
			if( getmapusers( "morocc" ) == 0 )
				.@none++;
		}
		
		if( .@none >= 5 )
		{
			$EVENT_STATE[EVENTID_PORINGPOINT] = 0;
			
			bot_announce( "Que pena! Não tivemos nenhum vencedor na **Moeda Poring**!", CHANNEL_GERAL );
			announce "[Moeda Poring]: Que pena! Não tivemos nenhum vencedor!",0;
		}
		
		killmonster "morocc","Saída do Evento#zp::OnPoring";
		killmonster "morocc","Saída do Evento#zp::OnPoringTrap";
		end;

	OnPoringTrap:
		atcommand "@die";
		end;

	OnPoring:
		if( !$EVENT_STATE[EVENTID_PORINGPOINT] ) end;
		
		setarray .@idItens[0],29510,29511,29512,29513,29514,29515,29516,29517,29518;
		setarray .@chance[0],15,7,20,40,60,10,15,15,20;

		.@moeda = 29509;

		if(rand(1,5) != 1 && @chanceZP < 2)
		{
			message strcharinfo(0),"Que pena, nada aqui!";
			end;
		}
		else
			@chanceZP++;

		@chanceZP = 0;

		if(rand(1,2) == 1)
		{
			getitem 29509,1;
			sleep2 5000;
			announce "[Moeda Poring]: " + strcharinfo(0) + " conseguiu uma moeda poring!",bc_map;
			end;
		}
		else
			message strcharinfo(0),"Que pena, nenhuma moeda aqui!";

		while(.@i < 5)
		{
			.@i++;
			.@dropItem = rand(getarraysize(.@idItens));
			if(rand(.@chance[.@dropItem]) > rand(100))
			{
				announce "[Moeda Poring]: " + strcharinfo(0) + " conseguiu um item do evento!",bc_map;
				getitem .@idItens[.@dropItem],1;
				end;
			}
		}
		end;
}

//ARG0 - Item DB
function	script	mp_use	{

	.@item = getarg(0);

	if(strcharinfo(3) != "morocc")
	{
		message strcharinfo(0),"Este item não pode ser usado aqui";
		end;
	}

	if( !$EVENT_STATE[EVENTID_PORINGPOINT] )
	{
		message strcharinfo(0),"O evento acabou!";
		end;
	}

	//Moeda Poring
	if(.@item == 29509)
	{
		PONTOS_PORING++;

		l_pontos:
		if(PONTOS_PORING >= 10)
		{
			$EVENT_STATE[EVENTID_PORINGPOINT] = 0;
			announce "[Moeda Poring]: " + strcharinfo(0) + " acaba de ganhar o evento Moeda Poring!",0;
			$MP_LASTOWNER$ = strcharinfo(0);
			pvpoff "morocc";
			killmonster "morocc","Saída do Evento#zp::OnPoring";
			killmonster "morocc","Saída do Evento#zp::OnPoringTrap";
			
			.@rand = rand(75,120);
			
			if(getmasterylevel( 200, getcharid(0) ) >= 100)
				.@rand += (.@rand * 20) / 100;
			
			#EVENT_POINTS += .@rand;
			dispbottom "--------------------------------------------------";
			dispbottom "Você recebeu " + .@rand + " pontos de evento!";
			dispbottom "Total de pontos: " + #EVENT_POINTS;
			dispbottom "--------------------------------------------------";
			
			#CASHPOINTS += 100;

			bot_announce( "**" + strcharinfo(0) + "** acaba de ganhar o evento **Moeda Poring**!", CHANNEL_GERAL );

			addrid(1);
			sleep2 5000;
			warp "Save",0,0;
			end;
		}

		message strcharinfo(0),"Você está com (" + PONTOS_PORING + ") pontos";
		end;
	}

	//Bomba Poring
	if(.@item == 29510)
	{
		.@used = getcharid(0);
		announce "[Moeda Poring]: " + strcharinfo(0) + " utilizou uma [Bomba Poring]!",bc_map;
		addrid(1);
		if(.@used == getcharid(0)) end;
		//showdamage 900,getcharid(0),1,1;
		bonus_script "bonus bSpeedAddRate,-30;",7,3;
		specialeffect2 38;
		sleep2 7000;
		specialeffect2 37;
		end;
	}

	//Portal Poring
	if(.@item == 29511)
	{
		$USED_PORTAL = getcharid(0);
		announce "[Moeda Poring]: " + strcharinfo(0) + " utilizou um [Portal Poring]!",bc_map;
		addrid(1);
		if( $USED_PORTAL == getcharid(0) ) end;
		@MOEDA_TELEPORTE = 1;
		warp "Save",0,0;
		end;
	}

	//Armadilha Poring
	if(.@item == 29512)
	{
		getmapxy(.@m$,.@x,.@y,0);
		announce "[Moeda Poring]: " + strcharinfo(0) + " utilizou uma [Armadilha Poring]!",bc_map;
		monster "morocc",.@x,.@y,"Poring >:3",1725,1,"Saída do Evento#zp::OnPoringTrap";
		end;
	}

	//Salto Poring
	if(.@item == 29513)
	{
		warp strcharinfo(3),0,0;
		announce "[Moeda Poring]: " + strcharinfo(0) + " utilizou um [Salto Poring]!",bc_map;
		end;
	}

	//Dica Poring
	if(.@item == 29514)
	{
		announce "[Moeda Poring]: " + strcharinfo(0) + " utilizou uma [Dica Poring]!",bc_map;
		atcommand "@showmobs " + 1725;
		atcommand "@showmobs " + 1002;
		end;
	}

	//Trapaceiro Poring
	if(.@item == 29515)
	{
		announce "[Moeda Poring]: " + strcharinfo(0) + " utilizou um [Trapaça Poring]!",bc_map;
		.@select = select("Remover Pontos:Remover Moedas");
		.@used = getcharid(0);

		if(.@select == 1)
		{
			addrid(1);
			if(.@used == getcharid(0)) end;
			if(PONTOS_PORING <= 2 ) PONTOS_PORING = 0;
			else PONTOS_PORING -= 2;
			message strcharinfo(0),"Você agora tem ( " + PONTOS_PORING + ") pontos";
			end;
		}
		else
		{
			addrid(1);
			if(.@used == getcharid(0)) end;
			delitem 29509,2;
			message strcharinfo(0),"Você agora tem ( " + countitem(29509) + ") moedas";
			end;
		}
		end;
	}

	//Guerra Poring
	if(.@item == 29516)
	{
		announce "[Moeda Poring]: " + strcharinfo(0) + " utilizou um [Guerra Poring]!",bc_map;
		removemapflag "morocc",mf_noskill;
		setmapflag "morocc",mf_pvp;
		end;
	}

	//Paz Poring
	if(.@item == 29517)
	{
		if(getmapflag(strcharinfo(3),mf_pvp))
		{
			.@rand = rand(1,100);
			if(.@rand <= 5)
			{
				removemapflag "morocc",mf_pvp;
				setmapflag "morocc",mf_noskill;
		
				PONTOS_PORING += 2;
				goto l_pontos;
			}
		}
		pvpoff "morocc";
		announce "[Moeda Poring]: " + strcharinfo(0) + " utilizou um [Paz Poring]!",bc_map;
		end;
	}

	//Troll Poring
	if(.@item == 29518)
	{
		announce "[Moeda Poring]: " + strcharinfo(0) + " utilizou um [Troll Poring]!",bc_map;
		addrid(1);
		atcommand "@disguise Poring";
		sleep2 30000;
		atcommand "@undisguise";
		end;
	}
	end;
}