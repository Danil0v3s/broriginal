
-	Script	Condecoração de Classes	10184,{

	//sc_start SC_FREEZE,240000,10;

	//end;

	.@n$ = "[^4682B4Condecoração de Classes^000000]";
	
	cutin "mestra_classe",2;
	
	mes .@n$;
	
	if( JobLevel < 10 && Class == 0 )
	{
		mes "Volte quando estiver com nível de classe igual a 10.";
		mes "Certifique-se de gastar também seus pontos de habilidades.";
		
		L_whereLeveling:
		
		if( select( Sex == 0 ? "Obrigada" : "Obrigado", "Onde consigo evoluir de nível?" ) == 1 )
		{
			close2;
			cutin "mestra_classe",255;
			end;
		}
		
		next;
		mes .@n$;
		mes "Isso é fácil, basta apenas falar com o ^B03060Guardião da Torre da Jornada^000000 e entrar na torre referente ao seu nível atual.";
		next;
		mes .@n$;
		mes "Irei lhe mostrar no mapa a localização dele. Boa sorte!";
		close2;
		cutin "mestra_classe",255;
		viewpoint 1,332,343,1,0xFF0000;
		end;
	}
	
	if( SkillPoint > 0 )
		atcommand "@allskills";

	if( Sex == 1 )
		setarray .@jobs$[1],"Espadachim","Mago","Arqueiro","Mercador","Gatuno","Noviço","Taekwon","Ninja","Justiceiro","Super Aprendiz";
	else
		setarray .@jobs$[1],"Espadachim","Maga","Arqueira","Mercadora","Gatuna","Noviça","Taekwon","Kunoichi","Justiceira","Super Aprendiza";
	
	setarray .@jobs_id[1],1,2,3,5,6,4,4046,25,24,23;
		
	if( Class == 25 || Class == 24 || Class == 23 || Class == 4047 || Class == 4049 )
	{
		mes "Ainda não possuo uma nova classe para você. Quem sabe no futuro?";
		close2;
		cutin "mestra_classe",255;
		end;
	}
	
	if( REBIRTH == 1 )
	{
		mes "Existe uma lenda de que se você alcançar um nível alto, poderá adquirir um novo poder.";
		close2;
		cutin "mestra_classe",255;
		end;
	}
	
	if( REBIRTH == 2 )
	{
		mes "Quem diria que você se tornaria tão forte!";
		close2;
		npctalk "Boa Jornada, " + strcharinfo(0) + "!";
		cutin "mestra_classe",255;
		goto L_PlatSkills;
		end;
	}
	
	if( inarray( .@jobs_id, Class ) >= 1 )
	{
		if( JobLevel < 50 )
		{
			mes "Volte quando estiver com nível de classe igual a 50.";
			goto L_whereLeveling;
		}
		
		.@myJob = inarray( .@jobs_id, Class );
		
		mes "Você realmente está se destacando. Merece uma promoção.";
		mes "Por favor, escolha o que deseja se tornar.";
		
		if( Sex == 1 )
			setarray .@jobs_evo$[1],"Cavaleiro:Templário",
			                        "Bruxo:Sábio",
									"Caçador:Bardo",
									"Ferreiro:Alquimista",
									"Mercenário:Arruaceiro",
									"Sacerdote:Monge",
									"Mestre Taekwon:Espiritualista";
		else
			setarray .@jobs_evo$[1],"Cavaleira:Templária",
			                        "Bruxa:Sábia",
									"Caçadora:Odalisca",
									"Ferreira:Alquimista",
									"Mercenária:Arruaceira",
									"Sacerdotiza:Monja",
									"Mestra Taekwon:Espiritualista";		
		
		setarray .@jobs_id$[1],"7:14",
		                      "9:16",
							  "11:19",
							  "10:18",
							  "12:17",
							  "8:15",
							  "4047:4049";
							  
		.@option = select( .@jobs_evo$[.@myJob] );
		.@option--;
		
		explode( .@jobs_data_name$, .@jobs_evo$[.@myJob], ":");
		explode( .@jobs_data$, .@jobs_id$[.@myJob], ":");

		.@newJob = atoi( .@jobs_data$[.@option] );
		
		.@jobs$[.@option] = .@jobs_data_name$[.@option];
		
		if( .@newJob == 19 && Sex == 0 )
			.@newJob = 20;
			
		.@REBIRTH = .@newJob >= 4047 ? 0 : 1;
		
		next;
		goto L_confirm;
	}
	
	mes "Você mostrou que tem talento lá na ^B03060Torre da Jornada^000000. Decidiu qual será sua clase?";
	
	.@option = select( implode( .@jobs$, ":" ) );
	.@option--;
	
	.@newJob = .@jobs_id[.@option];
	
	next;
	
	L_confirm:
	mes .@n$;
	mes "Certo, então deseja se tornar " + ( !Sex ? "uma" : "um" ) + " " + .@jobs$[.@option] + "?";

	if( select( "Irei pensar mais sobre.", "Sim, por favor." ) == 2 )
	{
		close2;
		jobchange .@newJob;
		cutin "mestra_classe",255;
		npctalk "Boa Sorte " + strcharinfo(0) + "! Sei que você será " + ( !Sex ? "uma magnífica " : "um excelente " ) + .@jobs$[.@option] + "!";
		
		if( .@REBIRTH )
			REBIRTH = 1;
			
		goto L_PlatSkills;
		end;
	}
	
	next;
	mes .@n$;
	mes "Tudo bem, é sempre bom pensar antes de tomar uma decisão de carreira.";
	close2;
	cutin "mestra_classe",255;
	end;
	
	L_PlatSkills:
		rpc( getcharid(0) );;
		atcommand "@allskills";
		skill 142,1,0;
		switch (BaseClass) {
			case Job_Novice:
				if (Class != Job_Super_Novice)
					skill "NV_TRICKDEAD",1,SKILL_PERM;
				break;
			case Job_Swordman:
				skill "SM_MOVINGRECOVERY",1,SKILL_PERM;
				skill "SM_FATALBLOW",1,SKILL_PERM;
				skill "SM_AUTOBERSERK",1,SKILL_PERM;
				break;
			case Job_Mage:
				skill "MG_ENERGYCOAT",1,SKILL_PERM;
				break;
			case Job_Archer:
				skill "AC_MAKINGARROW",1,SKILL_PERM;
				skill "AC_CHARGEARROW",1,SKILL_PERM;
				break;
			case Job_Acolyte:
				skill "AL_HOLYLIGHT",1,SKILL_PERM;
				break;
			case Job_Merchant:
				skill "MC_CARTREVOLUTION",1,SKILL_PERM;
				skill "MC_CHANGECART",1,SKILL_PERM;
				skill "MC_LOUD",1,SKILL_PERM;
				break;
			case Job_Thief:
				skill "TF_SPRINKLESAND",1,SKILL_PERM;
				skill "TF_BACKSLIDING",1,SKILL_PERM;
				skill "TF_PICKSTONE",1,SKILL_PERM;
				skill "TF_THROWSTONE",1,SKILL_PERM;
				break;
			default:
				break;
			}
			switch (BaseJob) {
			case Job_Knight:
				skill "KN_CHARGEATK",1,SKILL_PERM;
				break;
			case Job_Priest:
				skill "PR_REDEMPTIO",1,SKILL_PERM;
				break;
			case Job_Wizard:
				skill "WZ_SIGHTBLASTER",1,SKILL_PERM;
				break;
			case Job_Blacksmith:
				skill "BS_UNFAIRLYTRICK",1,SKILL_PERM;
				skill "BS_GREED",1,SKILL_PERM;
				break;
			case Job_Hunter:
				skill "HT_PHANTASMIC",1,SKILL_PERM;
				break;
			case Job_Assassin:
				skill "AS_SONICACCEL",1,SKILL_PERM;
				skill "AS_VENOMKNIFE",1,SKILL_PERM;
				break;
			case Job_Crusader:
				skill "CR_SHRINK",1,SKILL_PERM;
				break;
			case Job_Monk:
				skill "MO_KITRANSLATION",1,SKILL_PERM;
				skill "MO_BALKYOUNG",1,SKILL_PERM;
				break;
			case Job_Sage:
				skill "SA_CREATECON",1,SKILL_PERM;
				skill "SA_ELEMENTWATER",1,SKILL_PERM;
				skill "SA_ELEMENTGROUND",1,SKILL_PERM;
				skill "SA_ELEMENTFIRE",1,SKILL_PERM;
				skill "SA_ELEMENTWIND",1,SKILL_PERM;
				break;
			case Job_Rogue:
				skill "RG_CLOSECONFINE",1,SKILL_PERM;
				break;
			case Job_Alchemist:
				skill "AM_BIOETHICS",1,SKILL_PERM;
				break;
			case Job_Bard:
				skill "BA_PANGVOICE",1,SKILL_PERM;
				break;
			case Job_Dancer:
				skill "DC_WINKCHARM",1,SKILL_PERM;
				break;
			default:
				break;
			}
		end;
		
	OnPCLoginEvent:
		atcommand "@allskills";
	OnPCBaseLvUpEvent:
		if( BaseLevel >= 200 && REBIRTH == 1 )
		{
			announce "[Condecoração de Classe]: " + strcharinfo(0) + " adquiriu o poder dos Deuses.",0;
			jobchange Class + 4001;
			REBIRTH = 2;
			StatusPoint += 120;
			
			dispbottom "Parabéns por atingir o nível " + BaseLevel + "! Você recebeu 120 pontos de atributo!";
			
			goto L_PlatSkills;
		}
		end;
}

phtownall,196,186,5	duplicate(Condecoração de Classes)	Condecoração de Classes#PRT	10184
