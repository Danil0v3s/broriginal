
function	script	mastery	{

	.@func$ = getarg(0);
	.@cid = getarg(1);

	if( .@cid == 0 ) return;

	if( !#MASTERY_FT )
	{
		query_sql( sprintf( "INSERT INTO `mastery_player` ( `bound`, `mastery_id`, `level` ) " +
					        "SELECT %d AS  `bound`, `id`, 1 AS `lvl`                           " +
					        " FROM `mastery`                                                 " +
					        " WHERE `type` = '1' ",
					        getcharId(3) ) );

		#MASTERY_FT = 1;
	}

	if( .@func$ == "GiveMastery" )
	{
		.@mast_id = getarg(2);
		.@item_id = getarg(3);

		.@my_mastlevel = getmasterylevel( .@mast_id, .@cid ) + 1;

		.@c = query_sql("SELECT `max_level`, `nome` " +
			            "  FROM `mastery` " +
			            " WHERE `id` = " + .@mast_id, .@max_level, .@nome$ );

		if( .@my_mastlevel > .@max_level )
		{
			message strcharinfo(0),"Maestria já está no nível máximo";
			getitem .@item_id,1;
			end;
		}

		bot_announce( "**" + strcharinfo(0) + "** acaba de adquirir a maestria __" + getitemname( .@item_id ) + "__", CHANNEL_CONQUISTAS );

		if( .@my_mastlevel == 1 )
		{
			query_sql( 
				sprintf( "INSERT INTO `mastery_player` ( `bound`, `mastery_id`, `level` ) " +
					 	 "     VALUES ( '%d', '%d', '%d' ) ", .@cid, .@mast_id, .@my_mastlevel ) );
		}
		else
		{
			query_sql( "UPDATE `mastery_player` " +
			           "   SET `level` = " + .@my_mastlevel +
			           " WHERE `bound` = " + .@cid +
			           "   AND `mastery_id` = " + .@mast_id );
		}

		dispbottom "A maestria [" + .@nome$ + "] agora está no nível (" + .@my_mastlevel + ").";

		apply_mastery( .@mast_id, .@my_mastlevel, .@cid, 1 );

		return;
	}

	if( .@func$ == "LoadPlayerData" || .@func$ == "SendPlayerData" )
	{
		if( !FIRST_MASTERY_RECEIVED )
		{
			.@class = Class;

			if( .@class == 4021 )
				.@class = 4020;

			if( .@class == 4048 )
				.@class = 4047;

			// Super Aprendizes recebem na gambiarra mesmo
			if( .@class == 23 )
			{
				query_sql( 
						sprintf( "INSERT INTO `mastery_player` ( `bound`, `mastery_id`, `level` ) " +
					             "SELECT %d AS `bound`, `id`, 1 AS `lvl`                              " +
					             " FROM `mastery`                                                   " +
					             " WHERE `job` IN ( %d, %d, %d, %d, %d, %d )                        ",
					             .@cid, JOB_SWORDMAN, JOB_MAGE, JOB_ARCHER, JOB_ACOLYTE, JOB_MERCHANT, JOB_THIEF ) );
			}
			else
			{
				query_sql( 
						sprintf( "INSERT INTO `mastery_player` ( `bound`, `mastery_id`, `level` ) " +
					             "SELECT %d AS  `bound`, `id`, 1 AS `lvl`                           " +
					             " FROM `mastery`                                                 " +
					             " WHERE `job` IN ( %d, %d, %d )                                  ",
					             .@cid, BaseClass, BaseJob, .@class ) );
			}

			FIRST_MASTERY_RECEIVED = 1;
		}

		.@c = query_sql( "SELECT `mastery_id`, `level` " +
		                 "  FROM `mastery_player`      " +
		                 " WHERE `bound` = " + getcharid(0) +
		                 "    OR `bound` = " + getcharid(3), .@mastery_id, .@level );
			
		if( .@c == 0 )
		{
			debugmes "Mastery", "Erro ao carregar maestrias do CID " + .@cid;
			end;
		}

		.@send_data = ( .@func$ == "SendPlayerData" );

		for( .@i = 0; .@i < .@c; .@i++ )
			apply_mastery( .@mastery_id[.@i], .@level[.@i], .@cid, .@send_data );

		return;
	}
	else if( .@func$ == "UpMastery" )
	{
		.@mastery_id = getarg(2);
		.@mastery_newlevel = getarg(3);

		.@c = query_sql("SELECT `type`    " +
			            "  FROM `mastery` " +
			            " WHERE `id` = " + .@mastery_id, .@mast_type);

		//announce "Mastery ID " + .@mastery_id, 0;
		//announce "Mastery New Level " + .@mastery_newlevel, 0;
		//announce "Mastery Type " + .@mast_type, 0;

		if( .@c != 1 )
		{
			debugmes "Mastery","Erro ao encontrar maestria com ID " + .@mastery_id;
			end;
		}

		if( .@mast_type == 1 )
			.@refid = getcharid(3);
		else
			.@refid = getcharid(0);

		.@c = query_sql( "SELECT m.nome, mp.level, m.max_level, m.type        " +
		                 "  FROM `mastery_player` mp                          " +
		                 " INNER JOIN `mastery` m ON m.id = mp.mastery_id     " +
		                 " WHERE mp.bound = " + .@refid +
		                 "   AND mp.mastery_id = " + .@mastery_id + "", .@nome$, .@level, .@max_level, .@type );

		if( .@c != 1 || .@mastery_newlevel < .@level )
		{
			debugmes "Mastery","( " + .@c + ")Erro ao comprar maestrias do CID " + .@cid + "(.@mastery_id = " + .@mastery_id + " / .@mastery_newlevel = " + .@mastery_newlevel + ")";
			end;
		}

		.@increasing = ( .@mastery_newlevel - .@level );

		if( ( .@level + .@increasing ) > .@max_level )
		{
			debugmes "Mastery","Tentativa de compra de maestria em nível superior com CID " + .@cid;
			
			.@increasing = .@max_level - .@level;
		}

		// 150 -> 50
		// 151 -> 50
		// 152 -> 50
		.@lvl = .@level;

		freeloop(1);

		while( .@lvl != .@mastery_newlevel )
		{
			.@lvl++;
			.@calc = ( .@lvl / 8 );

			.@cost += .@calc > 20 ? 20 : .@calc;
		}

		// Vamos descobrir se o jogador pode arcar com os custos ou se tá chitado
		switch(.@type)
		{
			case 0: // Pontos de Evento				
				if( #EVENT_POINTS < .@cost )
				{
					debugmes "Mastery","Tentativa de compra de maestria sem pontos de evento suficientes com CID " + .@cid;
					end;
				}

				#EVENT_POINTS -= .@cost;

				break;

			case 1: // Pontos de Fidelidade
				if( #ONLINE_POINTS < .@cost )
				{
					debugmes "Mastery","Tentativa de compra de maestria sem pontos de Fidelidade suficientes com CID " + .@cid + sprintf( "(POINTS: %d | CUSTO: %d)", #ONLINE_POINTS, .@cost);
					debugmes "Mastery",sprintf( "Level Antigo -> %d | Novo Level Comprado -> %d", .@level, .@mastery_newlevel );
					end;					
				}

				#ONLINE_POINTS -= .@cost;

				break;

			case 2:
				if( @MASTERY_INSTANCE == 0 ) return;
				break;
		}

		query_sql( "UPDATE `mastery_player` " +
			       "   SET `level` = " + .@mastery_newlevel +
			       " WHERE `bound` = " + .@refid +
			       "   AND `mastery_id` = " + .@mastery_id );

		megdisp getcharid(0), "#MAST_" + .@mastery_id + "#A maestria [" + .@nome$ + "] agora está no nível (" + .@mastery_newlevel + ").";

		apply_mastery( .@mastery_id, .@mastery_newlevel, .@cid, 1 );
		return;
	}
}
